plugins {
  id 'java'
  id 'application'
  id 'org.sonarqube' version '2.1'
  id 'com.jfrog.bintray' version '1.8.0'
}

ext.moduleName = 'lavaplayer'
version = '1.3.7'

dependencies {
  compile 'com.sedmelluq:lavaplayer-common:1.0.6'
  compile 'com.sedmelluq:lavaplayer-natives:1.2.55'

  compile "org.slf4j:slf4j-api:$slf4jVersion"

  compile 'org.apache.httpcomponents:httpclient:4.5.5'
  compile 'commons-io:commons-io:2.6'

  compile 'com.fasterxml.jackson.core:jackson-core:2.9.4'
  compile 'com.fasterxml.jackson.core:jackson-databind:2.9.4'

  compile 'org.jsoup:jsoup:1.11.2'

  compile 'net.iharder:base64:2.3.9'
}

task updateVersion {
  new File("$projectDir/src/main/resources/com/sedmelluq/discord/lavaplayer/tools/version.txt").with {
    it.parentFile.mkdirs()
    it.text = version
  }
}

classes.finalizedBy(tasks['updateVersion'])

task sourceJar(type: Jar) {
  from sourceSets.main.allJava
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId rootProject.group
      artifactId moduleName

      from components.java

      artifact sourceJar {
        classifier 'sources'
      }
    }
  }
}

apply from: "${rootDir}/gradle/bintray.gradle"
