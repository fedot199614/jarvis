plugins {
  id 'de.undercouch.download' version '3.1.1'
}

import org.apache.tools.ant.taskdefs.condition.Os

import java.util.regex.Pattern

apply plugin: 'de.undercouch.download'

ext.opusVersion = '1.2.1'
ext.mpg123Version = '1.25.8'
ext.oggVersion = '1.3.3'
ext.vorbisVersion = '1.3.5'
ext.sampleRateVersion = '0.1.9'

task load { doLast {
  if (!file("$projectDir/samplerate/src").exists()) {
    def downloadPath = "$buildDir/tmp/libsamplerate.tar.gz"
    def unpackPath = "$buildDir/tmp"

    download {
      src "http://www.mega-nerd.com/SRC/libsamplerate-${sampleRateVersion}.tar.gz"
      dest downloadPath
    }

    copy {
      from tarTree(resources.gzip(downloadPath))
      into unpackPath
    }

    copy {
      from "$unpackPath/libsamplerate-${sampleRateVersion}/src"
      into "$projectDir/samplerate/src"
    }
  }

  if (!file("$projectDir/fdk-aac/libAACdec").exists()) {
    def downloadPath = "$buildDir/tmp/fdk-aac-master.zip"
    def unpackPath = "$buildDir"

    download {
      src 'https://github.com/mstorsjo/fdk-aac/archive/master.zip'
      dest downloadPath
    }

    copy {
      from zipTree(file(downloadPath))
      into unpackPath
    }

    copy {
      from "$unpackPath/fdk-aac-master"
      into "$projectDir/fdk-aac"
    }
  }

  if (!file("$projectDir/vorbis/libogg-${oggVersion}").exists()) {
    def downloadPath = "$buildDir/tmp/temp.zip"
    def unpackPath = "$buildDir"

    download {
      src "http://downloads.xiph.org/releases/ogg/libogg-${oggVersion}.zip"
      dest downloadPath
    }

    copy {
      from zipTree(file(downloadPath))
      into "$projectDir/vorbis"
    }

    download {
      src "http://downloads.xiph.org/releases/vorbis/libvorbis-${vorbisVersion}.zip"
      dest downloadPath
    }

    copy {
      from zipTree(file(downloadPath))
      into "$projectDir/vorbis"
    }
  }

  if (!file("$projectDir/opus/opus-${opusVersion}").exists()) {
    def downloadPath = "$buildDir/tmp/temp.tar.gz"
    def unpackPath = "$buildDir"

    download {
      src "http://downloads.xiph.org/releases/opus/opus-${opusVersion}.tar.gz"
      dest downloadPath
    }

    copy {
      from tarTree(file(downloadPath))
      into "$projectDir/opus"
    }
  }

  if (!Os.isFamily(Os.FAMILY_WINDOWS) && !file("$projectDir/mp3/mpg123-${mpg123Version}").exists()) {
    def downloadPath = "$buildDir/tmp/temp.tar.bz2"
    def unpackPath = "$buildDir"

    download {
      src "https://www.mpg123.de/download/mpg123-${mpg123Version}.tar.bz2"
      dest downloadPath
    }

    copy {
      from tarTree(file(downloadPath))
      into "$projectDir/mp3"
    }
  }
}}

ext.devEnvPath = null

def getDevEnvLocation() {
  if (ext.devEnvPath == null) {
    def testDirectory = file("$buildDir/tmp/vsloc")
    testDirectory.deleteDir()
    testDirectory.mkdirs()

    new File(testDirectory, "CMakeLists.txt") << ""

    def outStream = new StringBuilder()
    def cmakeProcess = "cmake .".execute(null as String[], testDirectory)
    cmakeProcess.consumeProcessOutput(outStream, outStream)
    cmakeProcess.waitFor()

    def matcher = Pattern.compile("working C compiler: ([^\\n]*) -- works").matcher(outStream)
    matcher.find()

    def baseDirectory = file(matcher.group(1))

    while (!new File(baseDirectory, 'Common7').directory && baseDirectory.parentFile?.directory) {
      baseDirectory = baseDirectory.parentFile
    }

    ext.devEnvPath = new File(baseDirectory, "Common7/IDE/devenv.exe").absolutePath
  }

  return ext.devEnvPath
}

def buildOpusOnWindows(bits, force) {
  def base = "$projectDir/libs/${bits}"
  def libs = ['opus']
  def present = libs.every { file("${base}/${it}.lib").exists() }

  if (force || !present) {
    file(base).deleteDir()
    file(base).mkdirs()

    def devenv = getDevEnvLocation()
    def platformName = bits == 64 ? "x64" : "Win32"

    def process = [devenv, "opus.sln", "/Project", "opus", "/Build", "Release|${platformName}"].
        execute(null as String[], file("$projectDir/opus/opus-${opusVersion}/win32/VS2015"))

    process.waitForProcessOutput(System.out as Appendable, System.err)

    libs.each {
      assert file("$projectDir/opus/opus-${opusVersion}/win32/VS2015/${platformName}/Release/${it}.lib").renameTo("${base}/${it}.lib")
    }
  }
}

def waitForAndCheckSuccess(process, name) {
  process.waitForProcessOutput(System.out as Appendable, System.err)

  if (process.exitValue() != 0) {
    throw new IllegalStateException("${name} failed.")
  }
}

def buildOpusOnLinux(bits, force) {
  def present = file("${projectDir}/libs/${bits}/libopus.a").exists()

  if (force || !present) {
    def flags = "-m${bits} -fPIC -O3"
    def process = ["./configure", "--enable-static", "--with-cpu=" + (bits == 32 ? "x86" : "x64-64"), "--with-pic", "CFLAGS=${flags}", "CXXFLAGS=${flags}", "LDFLAGS=${flags}"].
        execute(null as String[], file("$projectDir/opus/opus-${opusVersion}"))

    waitForAndCheckSuccess(process, "Opus ./configure")

    process = ["make", "clean"].execute(null as String[], file("$projectDir/opus/opus-${opusVersion}"))
    waitForAndCheckSuccess(process, "Opus make clean")

    process = ["make"].execute(null as String[], file("$projectDir/opus/opus-${opusVersion}"))
    waitForAndCheckSuccess(process, "Opus make")

    copy {
      from "$projectDir/opus/opus-${opusVersion}/.libs/libopus.a"
      into "$projectDir/libs/${bits}"
    }
  }
}

def buildMpg123OnLinux(bits, force) {
  def present = file("${projectDir}/libs/${bits}/libmpg123.a").exists()

  if (force || !present) {
    def process = ["patch", "-N", "mpg123-${mpg123Version}/configure", "configure.patch"].execute(null as String[], file("$projectDir/mp3"))
    process.waitForProcessOutput(System.out as Appendable, System.err)

    process = ["./configure", "--enable-static", "--with-cpu=" + (bits == 32 ? "x86" : "x86-64"), "--with-pic", "CFLAGS=-fPIC -O3 -m${bits}", "CXXFLAGS=-fPIC -O3 -m${bits}"].
        execute(null as String[], file("$projectDir/mp3/mpg123-${mpg123Version}"))

    waitForAndCheckSuccess(process, "Mpg123 ./configure")

    process = ["make", "clean"].execute(null as String[], file("$projectDir/mp3/mpg123-${mpg123Version}"))
    waitForAndCheckSuccess(process, "make clean")

    process = ["make"].execute(null as String[], file("$projectDir/mp3/mpg123-${mpg123Version}"))
    waitForAndCheckSuccess(process, "make")

    copy {
      from "$projectDir/mp3/mpg123-${mpg123Version}/src/libmpg123/.libs/libmpg123.a"
      into "$projectDir/libs/${bits}"
    }
  }
}

def prepareOggOnLinux(bits, force) {
  def configured = file("$projectDir/vorbis/libogg-${oggVersion}/include/ogg/config_types.h").exists()

  if (force || !configured) {
    def process = ["./configure", "--enable-static", "--build=x86_64-pc-linux-gnu", "--with-pic"].
        execute(null as String[], file("$projectDir/vorbis/libogg-${oggVersion}"))

    process.waitForProcessOutput(System.out as Appendable, System.err)
  }
}

def prepareBuilds(bits, force) {
  if (Os.isFamily(Os.FAMILY_WINDOWS)) {
    buildOpusOnWindows(bits, force)
  } else {
    prepareOggOnLinux(bits, force)
    buildOpusOnLinux(bits, force)
    buildMpg123OnLinux(bits, force)
  }
}

task compileNatives() { }
task checkNatives() { }

def buildTaskConfig = [
    buildBase: buildDir,
    projectBase: projectDir,
    deployBase: project(':natives-publish').projectDir,
    setupDependency: tasks.load,
    setupDoFirst: { config, bits -> prepareBuilds(bits, false) },
    compileTask: tasks.compileNatives,
    checkTask: tasks.checkNatives,
    name: 'connector'
]

createBuildTask(tasks, buildTaskConfig, 32)
createBuildTask(tasks, buildTaskConfig, 64)
